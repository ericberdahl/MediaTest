apply plugin: 'com.android.application'

def ndkDir
if (project.rootProject.file('local.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    ndkDir = properties.getProperty('ndk.dir')
}
if (!ndkDir) {
    ndkDir=System.getenv("ANDROID_NDK_HOME")
}

if(!ndkDir || ndkDir.empty) {
    throw new GradleException('Environment Variable ANDROID_NDK_HOME for NDK path need to be setup')
}

android {
    compileSdkVersion = 26

    defaultConfig {
        applicationId = 'com.adobe.eberdahl.mediatest'
        minSdkVersion 26
        targetSdkVersion  26
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_shared'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    sourceSets.main.jniLibs.srcDirs file(ndkDir).absolutePath +
            '/sources/third_party/vulkan/src/build-android/jniLibs'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
